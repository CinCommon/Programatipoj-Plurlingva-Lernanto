In this post, we'll look at how Terraform can be used to securely access S3 buckets from EC2 instances on AWS.

## Overview
Here's a high-level overview of what we're going to cover:

- Using IAM roles and policies to grant just enough permissions to EC2 instances to access S3 buckets
- Creating a VPC with public and private subnets for secure networking
- Launching an EC2 instance in the public subnet with an IAM role attached
- Writing a script to upload a file from the EC2 instance to an S3 bucket
Properly configuring IAM roles and VPC networking is crucial for security on AWS. The goal is to follow the principle of least privilege and avoid exposing resources publicly if not required.

## Terraform codes 
`bucket.tf`: Defines an S3 bucket resource. Although it currently only specifies the bucket's name, it serves as a placeholder for more advanced configurations.
```terraform
terraform {
  backend "s3" {
    #    region = var.AWS_REGION
    #  Initializing the backend...
    #│ Error: Variables not allowed
    #│   on backend.tf line 3, in terraform:
    #│    3:     region = var.AWS_REGION
    #│ Variables may not be used here.
    #    The reason for this limitation is that Terraform
    #    needs to configure the backend and initialize state
    #    before it can start evaluating variables and resources.
    #    So the backend block can't rely on variables being set already.
    region = "ap-southeast-2"
    bucket = "stevenyin-terraform-course-state-f21oq"
    key = "terraform/08-Userdata-and-volumes"
  }
}

resource "aws_s3_bucket" "mybucket-stevenyin-wi1n25" {
  bucket = "mybucket-stevenyin-wi1n25"
}
```

`iam.tf`: Establishes an IAM role and associated policies, granting permissions for S3 actions. This file ensures secure access to S3 resources via EC2 instances.
```terraform
# Defines an IAM role named "s3_bucket_role." This role is intended for EC2 instances and is associated with a managed policy ("AmazonS3ReadOnlyAccess"), granting read-only access to S3.
resource "aws_iam_role" "s3_bucket_role" {
  name               = "s3_bucket_role"
  managed_policy_arns = [data.aws_iam_policy.allow-list-s3-object.arn]
  assume_role_policy = <<EOF
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Action": "sts:AssumeRole",
      "Principal": {
        "Service": "ec2.amazonaws.com"
      },
      "Effect": "Allow",
      "Sid": ""
    }
  ]
}
EOF

}

# Creates an instance profile that is associated with the "s3_bucket_role." This profile allows the EC2 instances to assume the specified role.
resource "aws_iam_instance_profile" "s3_bucket_role_instance_profile" {
  name = "s3_bucket_role_instance_profile"
  role = aws_iam_role.s3_bucket_role.name
}

# Attaches a custom IAM policy to the "s3_bucket_role," granting permissions for various S3 actions. The policy specifies the exact resources (bucket and objects) to which the role has access.
resource "aws_iam_role_policy" "s3_bucket_role_policy" {
  name = "s3_bucket_role_policy"
  role = aws_iam_role.s3_bucket_role.id
  policy = <<EOF
{
    "Version": "2012-10-17",
    "Statement": [
        {
            "Effect": "Allow",
            "Action": [
              "s3:*"
            ],
            "Resource": [
              "arn:aws:s3:::${aws_s3_bucket.mybucket-stevenyin-wi1n25.bucket}",
              "arn:aws:s3:::${aws_s3_bucket.mybucket-stevenyin-wi1n25.bucket}/*"
            ]
        }
    ]
}
EOF

}

data "aws_partition" "current" {}

data "aws_iam_policy" "allow-list-s3-object" {
  arn = "arn:${data.aws_partition.current.partition}:iam::aws:policy/AmazonS3ReadOnlyAccess"
}
```


`instance.tf`: Sets up an EC2 instance, which will eventually have the ability to interact with the S3 bucket.
```terraform
resource "aws_instance" "example" {
  ami           = var.AMIs[var.AWS_REGION]
  instance_type = "t2.micro"

  # the VPC subnet
  subnet_id = aws_subnet.main-public-1.id

  # the security group
  vpc_security_group_ids = [aws_security_group.example-instance.id]

  # the public SSH key
  key_name = aws_key_pair.mykeypair.key_name

  # role:
  iam_instance_profile = aws_iam_instance_profile.s3_bucket_role_instance_profile.name
}

resource "aws_key_pair" "mykeypair" {
  key_name   = "mykeypair"
  public_key = file(var.PATH_TO_PUBLIC_KEY)
}
```

`securitygroup.tf`: Defines a security group that permits SSH access to the EC2 instance.
```terraform
resource "aws_security_group" "example-instance" {
  vpc_id      = aws_vpc.main.id
  name        = "allow-ssh"
  description = "security group that allows ssh and all egress traffic"
  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  ingress {
    from_port   = 22
    to_port     = 22
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }
  tags = {
    Name = "example-instance"
  }
}
```

`var.tf`: Contains variables used across the configuration, enhancing its modularity and flexibility.
```terraform
variable "AMIs" {
  type = map(string)
  default = {
    cn-north-1     = ""
    ap-southeast-2 = "ami-0d02292614a3b0df1"
    us-east-1      = "ami-0261755bbcb8c4a84"
  }
}

variable "AWS_REGION" {
  type    = string
  default = "ap-southeast-2"
}

variable "INSTANCE_DEVICE_NAME" {
  default = "/dev/xvdh"
}

variable "PATH_TO_PUBLIC_KEY" {
  type = string
}
```

`vpc.tf`: Sets up a VPC with subnets, routing tables, and an Internet Gateway to enable network connectivity.
```terraform
#: Establishes a VPC with a specified CIDR block, enabling DNS support and hostnames. This VPC serves as the foundational networking layer for the resources to be provisioned.
resource "aws_vpc" "main" {
  cidr_block           = "10.0.0.0/16"
  instance_tenancy     = "default"
  enable_dns_support   = "true"
  enable_dns_hostnames = "true"
  tags = {
    Name = "main"
  }
}

# : Defines both public and private subnets within the VPC. Public subnets are associated with route tables that have an Internet Gateway as the default route, allowing instances in these subnets to access the Internet.
resource "aws_subnet" "main-public-1" {
  vpc_id                  = aws_vpc.main.id
  cidr_block              = "10.0.1.0/24"
  map_public_ip_on_launch = "true"
  availability_zone       = "ap-southeast-2a"

  tags = {
    Name = "main-public-1"
  }
}

resource "aws_subnet" "main-public-2" {
  vpc_id                  = aws_vpc.main.id
  cidr_block              = "10.0.2.0/24"
  map_public_ip_on_launch = "true"
  availability_zone       = "ap-southeast-2b"

  tags = {
    Name = "main-public-2"
  }
}

resource "aws_subnet" "main-public-3" {
  vpc_id                  = aws_vpc.main.id
  cidr_block              = "10.0.3.0/24"
  map_public_ip_on_launch = "true"
  availability_zone       = "ap-southeast-2c"

  tags = {
    Name = "main-public-3"
  }
}

# Define private subnets within the VPC for internal resources
resource "aws_subnet" "main-private-1" {
  vpc_id                  = aws_vpc.main.id
  cidr_block              = "10.0.4.0/24"
  map_public_ip_on_launch = "false"
  availability_zone       = "ap-southeast-2a"

  tags = {
    Name = "main-private-1"
  }
}

resource "aws_subnet" "main-private-2" {
  vpc_id                  = aws_vpc.main.id
  cidr_block              = "10.0.5.0/24"
  map_public_ip_on_launch = "false"
  availability_zone       = "ap-southeast-2b"

  tags = {
    Name = "main-private-2"
  }
}

resource "aws_subnet" "main-private-3" {
  vpc_id                  = aws_vpc.main.id
  cidr_block              = "10.0.6.0/24"
  map_public_ip_on_launch = "false"
  availability_zone       = "ap-southeast-2c"

  tags = {
    Name = "main-private-3"
  }
}

# Creates an Internet Gateway and attaches it to the VPC. This gateway enables communication between instances in the public subnets and the Internet.
resource "aws_internet_gateway" "main-gw" {
  vpc_id = aws_vpc.main.id

  tags = {
    Name = "main"
  }
}

# Routes traffic from public subnets to the internet gateway
resource "aws_route_table" "main-public" {
  vpc_id = aws_vpc.main.id
  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.main-gw.id
  }

  tags = {
    Name = "main-public-1"
  }
}

# Associate the public route table with public subnets
resource "aws_route_table_association" "main-public-1-a" {
  subnet_id      = aws_subnet.main-public-1.id
  route_table_id = aws_route_table.main-public.id
}

# Associate the public route table with more public subnets
resource "aws_route_table_association" "main-public-2-a" {
  subnet_id      = aws_subnet.main-public-2.id
  route_table_id = aws_route_table.main-public.id
}

resource "aws_route_table_association" "main-public-3-a" {
  subnet_id      = aws_subnet.main-public-3.id
  route_table_id = aws_route_table.main-public.id
}
```

provider.tf
```terraform
provider "aws" {
  region = var.AWS_REGION
}
```

install-aws-cli.sh
```shell
#!/bin/bash

sudo apt-get update
sudo apt-get install -y python3-pip python-dev

sudo pip install awscli
```

## Uploading Files to S3
With the IAM role and VPC set up, we can now launch an EC2 instance and grant it access to S3.

A script is run on the instance to upload a file. The key steps are:

1. Install AWS CLI on the instance to get the S3 client tools
2. Authenticate using the attached IAM role
3. Upload a file to the S3 bucket using aws s3 cp

```shell
echo "Hello World!" >> test.log
aws s3 cp test.log s3://mybucket-stevenyin-wi1n25/test.log
```
![UPLOAD SUCCESS](./10.%20IAM.01.png)

The AWS CLI transparently handles authentication in the background using the metadata service. This lets any application running on the instance securely access other AWS services.

The script can also be packaged into the EC2 user data on launch for automatic execution. That way as soon as the instance starts, the S3 upload is kicked off.
